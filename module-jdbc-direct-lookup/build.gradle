buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'io.openliberty.tools:liberty-gradle-plugin:3.2.1'
    }
}

apply plugin: 'liberty'

//Liberty properties
ext  {
    liberty.server.var.'default.http.port' = '9080'
    liberty.server.var.'default.https.port' = '9443'
    liberty.server.var.'app.context.root' = project.name
}

//Open test results after completion of testing
task openResults {
    description = 'Open browser to the test results'
    doLast {
        java.awt.Desktop.desktop.browse file("$buildDir/reports/tests/test/index.html").toURI()
    }
}

//Open application during runtime
task openApp {
    description = 'Open browser to the running application'
    doLast {
        String port = liberty.server.var.'default.http.port'
        String context = liberty.server.var.'app.context.root'
        String URL = "http://localhost:" + port + "/" + context
        java.awt.Desktop.desktop.browse URL.toURI()
    }
} 

task network(type: Exec) {
    //Build and launch seperatly to avoid: https://github.com/docker/compose/issues/8121
    commandLine 'sh', '-c', 'docker network create test-net'
}

task database(type: Exec) {
	dependsOn network
    //Build and launch seperatly to avoid: https://github.com/docker/compose/issues/8121
    commandLine 'sh', '-c', 'docker run -d --network test-net --name oracle -p 1521:1521 -p 1522:1522 -p 5500:5500 -p 8080:8080 kyleaure/oracle-18.4.0-xe-prebuilt:2.0'
}

task appServer(type: Exec) {
	dependsOn network
    //Build and launch seperatly to avoid: https://github.com/docker/compose/issues/8121
    commandLine 'sh', '-c', 'docker build -t module-jdbc-direct-lookup:1.0 . && docker run -d --name module-jdbc-direct-lookup --network test-net -p 9080:9080 -p 9443:9443 module-jdbc-direct-lookup:1.0'
}

task dockerRun() {
	dependsOn build
	dependsOn appServer
	dependsOn database
	finalizedBy openApp
}

task dockerClean(type: Exec) {
	//Build and launch seperatly to avoid: https://github.com/docker/compose/issues/8121
    commandLine 'sh', '-c', 'docker stop module-jdbc-direct-lookup oracle && docker rm module-jdbc-direct-lookup oracle && docker network rm test-net && docker rmi -f module-jdbc-direct-lookup:1.0'
}

//Testing config
test {
    // Use junit platform for unit tests
    useJUnitPlatform()

    //Logging parameters for testing
    testLogging {
        events 'passed', 'skipped', 'failed', 'standardOut'
        exceptionFormat 'full'
    }

    //Liberty settings for testing
    systemProperty 'http.port', liberty.server.var.'default.http.port'
    systemProperty 'context.root',  liberty.server.var.'app.context.root'
}


dependencies {
    providedCompile 'io.openliberty.features:jdbc-4.2:20.0.0.3'
    providedCompile 'io.openliberty.features:jaxrs-2.1:20.0.0.3'
    providedCompile 'io.openliberty.features:jaxrsClient-2.1:20.0.0.3'
    providedCompile 'io.openliberty.features:jndi-1.0:20.0.0.3'
    providedCompile 'io.openliberty.features:jsonp-1.1:20.0.0.3'
    providedCompile 'io.openliberty.features:servlet-4.0:20.0.0.3'
    
    testRuntimeOnly 'org.apache.cxf:cxf-rt-rs-client:3.3.6'
    testRuntimeOnly 'org.apache.cxf:cxf-rt-rs-extension-providers:3.3.6'
}

libertyDev.dependsOn copyJdbcDrivers
libertyStart.dependsOn copyJdbcDrivers

test.dependsOn libertyStart
test.finalizedBy openResults
test.finalizedBy libertyStop