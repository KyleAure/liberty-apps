apply plugin: 'liberty'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'io.openliberty.tools:liberty-gradle-plugin:3.0'
    }
}

//Liberty properties
ext  {
    liberty.server.var.'default.http.port' = '9080'
    liberty.server.var.'default.https.port' = '9443'
    liberty.server.var.'app.context.root' = project.name
}

task openApp {
    description = 'Open browser to the running application'
    doLast {
        String port = liberty.server.var.'default.http.port'
        String context = liberty.server.var.'app.context.root'
        String URL = "http://localhost:" + port + "/" + context
        java.awt.Desktop.desktop.browse URL.toURI()
    }
}

test {
    systemProperty 'http.port', liberty.server.var.'default.http.port'
    systemProperty 'context.root',  liberty.server.var.'app.context.root'
}

dependencies {
    // Test compile dependencies
    testCompile 'org.apache.cxf:cxf-rt-rs-client:3.3.6'
    testCompile 'org.apache.cxf:cxf-rt-rs-extension-providers:3.3.6'
    testCompile 'org.glassfish:javax.json:1.1.4'
    // Provided APIs supplied by Liberty
    providedCompile 'jakarta.platform:jakarta.jakartaee-api:8.0.0'
    providedCompile 'org.eclipse.microprofile:microprofile:3.3'
}

test.dependsOn 'libertyStart'
test.finalizedBy 'libertyStop'
