apply plugin: 'liberty'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'io.openliberty.tools:liberty-gradle-plugin:3.0'
    }
}

//Liberty properties
ext  {
    liberty.server.var.'default.http.port' = '9080'
    liberty.server.var.'default.https.port' = '9443'
    liberty.server.var.'app.context.root' = project.name
}

task openApp {
    description = 'Open browser to the running application'
    doLast {
        String port = liberty.server.var.'default.http.port'
        String context = liberty.server.var.'app.context.root'
        String URL = "http://localhost:" + port + "/" + context
        java.awt.Desktop.desktop.browse URL.toURI()
    }
}

test {
    systemProperty 'http.port', liberty.server.var.'default.http.port'
    systemProperty 'context.root',  liberty.server.var.'app.context.root'
}

dependencies {
    testCompile 'org.slf4j:slf4j-log4j12:1.7.30'
    testCompile 'org.microshed:microshed-testing-testcontainers:0.8'

    compile 'org.jnosql.artemis:artemis-document:0.0.9'
    compile 'org.jnosql.diana:mongodb-driver:0.0.9'

    providedCompile 'javax:javaee-api:8.0.1'
    providedCompile 'org.eclipse.microprofile:microprofile:3.3'

    providedCompile 'io.openliberty.features:jaxrs-2.1:21.0.0.4'
}

test.dependsOn 'libertyStart'
test.finalizedBy 'libertyStop'

