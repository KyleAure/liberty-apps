allprojects {
    //Group and version information for sub projects
    group = 'websphereapps'
    version = '1.0'
}

// configure liberty-gradle-plugin
buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        classpath 'io.openliberty.tools:liberty-gradle-plugin:3.0'
    }
}

subprojects {
    //Plugins for all sub projects
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'liberty'
    apply plugin: 'war'

    //General properties
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    compileJava.options.encoding = 'UTF-8'
    compileTestJava.options.encoding = 'UTF-8'

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    //Maven central repository for dependencies
    repositories {
        mavenCentral()
    }

    //Liberty properties
    ext  {
        liberty.server.var.'default.http.port' = '9080'
        liberty.server.var.'default.https.port' = '9443'
        liberty.server.var.'app.context.root' = project.name
    }

    task openBrowser {
        description = 'Open browser to the running application'
        doLast {
            String port = liberty.server.var.'default.http.port'
            String context = liberty.server.var.'app.context.root'
            String URL = "http://localhost:" + port + "/" + context
            java.awt.Desktop.desktop.browse URL.toURI()
            java.awt.Desktop.desktop.browse file("$buildDir/reports/tests/test/index.html").toURI()
        }
    }

    test {
        useJUnitPlatform()
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut'
            exceptionFormat 'full'
        }
        systemProperty 'http.port', liberty.server.var.'default.http.port'
        systemProperty 'context.root',  liberty.server.var.'app.context.root'
    }

    test.dependsOn 'libertyStart'
    test.finalizedBy(openBrowser)
    clean.dependsOn 'libertyStop'

    configurations {
        jdbcDrivers { transitive = false }
    }

    dependencies {
        jdbcDrivers 'com.ibm.db2:jcc:11.5.0.0',
                'org.postgresql:postgresql:42.2.12',
                'com.microsoft.sqlserver:mssql-jdbc:7.4.1.jre8',
                'org.apache.derby:derbyclient:10.15.2.0',
                'org.apache.derby:derby:10.15.2.0',
                'com.oracle.ojdbc:ojdbc8_g:19.3.0.0'
    }

    task copyJdbcDrivers(type: Copy) {
        from configurations.jdbcDrivers
        into 'src/main/liberty/config/resources'
        rename 'jcc.*.jar', 'jcc.jar'
        rename 'derby-.*.jar', 'derby.jar'
        rename 'derbyclient-.*.jar', 'derbyclient.jar'
        rename 'ojdbc8_g.*.jar', 'ojdbc8_g.jar'
        rename 'postgresql.*.jar', 'postgresql.jar'
        rename 'mssql-jdbc.*.jar', 'mssql-jdbc.jar'
    }
}
