allprojects {
    apply plugin: 'java'

    //Group and version information for sub projects
    group = 'websphereapps.$task.project.name'
    version = '1.0'

    //General properties
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    compileJava.options.encoding = 'UTF-8'
    compileTestJava.options.encoding = 'UTF-8'
}

subprojects {
    apply plugin: 'war'
    
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    task openResults {
        description = 'Open browser to the test results'
        doLast {
            java.awt.Desktop.desktop.browse file("$buildDir/reports/tests/test/index.html").toURI()
        }
    }

    test {
        // Use junit platform for unit tests
        useJUnitPlatform()

        // Always run tests, even when nothing changed.
        dependsOn 'cleanTest'

        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut'
            exceptionFormat 'full'
        }
    }

    repositories {
        mavenCentral()
    }

    configurations {
        jdbcDrivers { transitive = false }
    }

    dependencies {
        // Use JUnit Jupiter API for testing.
        testCompile 'org.junit.jupiter:junit-jupiter-api:5.6.2'
        testCompile 'org.junit.jupiter:junit-jupiter-params:5.6.2'

        // Use JUnit Jupiter Engine for testing.
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.2'
        jdbcDrivers 'com.ibm.db2:jcc:11.5.0.0',
                'org.postgresql:postgresql:42.2.12',
                'com.microsoft.sqlserver:mssql-jdbc:9.3.0.jre8-preview',
                'org.apache.derby:derbyclient:10.15.2.0',
                'org.apache.derby:derby:10.15.2.0',
                'com.oracle.ojdbc:ojdbc8_g:19.3.0.0'
    }

    task copyJdbcDrivers(type: Copy) {
        from configurations.jdbcDrivers
        into 'src/main/liberty/config/resources'
        rename 'jcc.*.jar', 'jcc.jar'
        rename 'derby-.*.jar', 'derby.jar'
        rename 'derbyclient-.*.jar', 'derbyclient.jar'
        rename 'ojdbc8_g.*.jar', 'ojdbc8_g.jar'
        rename 'postgresql.*.jar', 'postgresql.jar'
        rename 'mssql-jdbc.*.jar', 'mssql-jdbc.jar'
    }

    test.finalizedBy(openResults)
}
