subprojects {
    apply plugin: 'java'
    apply plugin: 'war'

    //Group and version information for sub projects
    group = 'liberty.app.$project.name'
    version = '1.0'

    //General properties
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    compileJava.options.encoding = 'UTF-8'
    compileTestJava.options.encoding = 'UTF-8'

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    repositories {
        mavenCentral()
    }

    configurations {
        jdbcDrivers { transitive = false }
    }

    dependencies {
        // Use JUnit Jupiter API for testing.
        testCompile 'org.junit.jupiter:junit-jupiter-api:5.6.2'
        testCompile 'org.junit.jupiter:junit-jupiter-params:5.6.2'

        // Use JUnit Jupiter Engine for testing.
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.2'
        jdbcDrivers 'com.ibm.db2:jcc:11.5.0.0',
                'org.postgresql:postgresql:42.2.12',
                'com.microsoft.sqlserver:mssql-jdbc:9.3.1.jre8-preview',
                'org.apache.derby:derbyclient:10.11.1.1',
                'org.apache.derby:derby:10.11.1.1',
                'com.oracle.ojdbc:ojdbc8_g:19.3.0.0'
    }

    task copyJdbcDrivers(type: Copy) {
        from configurations.jdbcDrivers
        into "src/main/liberty/config/resources/jdbc"
        rename 'jcc.*.jar', 'jcc.jar'
        rename 'derby-.*.jar', 'derby.jar'
        rename 'derbyclient-.*.jar', 'derbyclient.jar'
        rename 'ojdbc8_g.*.jar', 'ojdbc8_g.jar'
        rename 'postgresql.*.jar', 'postgresql.jar'
        rename 'mssql-jdbc.*.jar', 'mssql-jdbc.jar'
    }
}