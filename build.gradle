allprojects {
    //Group and version information for sub projects
    group = 'websphereapps'
    version = '1.0'
}

subprojects {
    //Plugins for all sub projects
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'liberty'

    //General properties
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    compileJava.options.encoding = 'UTF-8'
    compileTestJava.options.encoding = 'UTF-8'

    //Liberty properties
    ext {
        appName = project.name
        testServerHttpPort = 9080
        testServerHttpsPort = 9443
        warContext = appName
    }

    //Maven central repository for dependencies
    repositories {
        mavenCentral()
    }

    //Liberty code
    liberty {
        server {
            name = "${appName}Server"
            configFile = file("src/main/liberty/config/server.xml")
            bootstrapProperties = ['default.http.port' : testServerHttpPort,
                                   'default.https.port': testServerHttpsPort,
                                   'app.context.root'  : warContext]
            packageLiberty {
                archive = "$buildDir/${appName}.zip"
                include = "usr"
            }
        }
    }

    test {
        defaultCharacterEncoding = "UTF-8"
        useJUnitPlatform()
        testLogging {
            displayGranularity 1
            showStandardStreams = true
            showStackTraces = true
            exceptionFormat = 'full'
            events 'PASSED', 'FAILED', 'SKIPPED'
        }
    }

    configurations {
        jdbcDrivers { transitive = false }
    }

    dependencies {
        jdbcDrivers 'com.ibm.db2:jcc:11.5.0.0',
                'org.postgresql:postgresql:42.2.10',
                'com.microsoft.sqlserver:mssql-jdbc:8.2.1.jre13',
                'org.apache.derby:derbyclient:10.15.2.0',
                'org.apache.derby:derby:10.11.1.1',
                'com.oracle.ojdbc:ojdbc8_g:19.3.0.0'

        testCompile "org.apache.cxf:cxf-rt-rs-client:3.3.5"
        testCompile "org.apache.cxf:cxf-rt-rs-extension-providers:3.3.5"
        testCompile "org.glassfish:javax.json:1.1.4"
        testCompile "javax.xml.bind:jaxb-api:2.3.1"
        testCompile "com.sun.xml.bind:jaxb-core:2.3.0.1"
        testCompile "com.sun.xml.bind:jaxb-impl:2.3.2"
        testCompile "javax.activation:activation:1.1.1"
    }

    task copyJdbcDrivers(type: Copy) {
        from configurations.jdbcDrivers
        into 'src/main/liberty/config/resources'
        rename 'jcc.*.jar', 'jcc.jar'
        rename 'derby-.*.jar', 'derby.jar'
        rename 'derbyclient-.*.jar', 'derbyclient.jar'
        rename 'ojdbc8_g.*.jar', 'ojdbc8_g.jar'
        rename 'postgresql.*.jar', 'postgresql.jar'
        rename 'mssql-jdbc.*.jar', 'mssql-jdbc.jar'
    }
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'net.wasdev.wlp.gradle.plugins:liberty-gradle-plugin:2.7'
    }
}


